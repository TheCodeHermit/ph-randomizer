area CourageTemple:
  room F1:
    node Entrance:
      door Outside
      chest AfterSpikeTraps

    node BehindBombableWall:
      chest BombableWallChest

    node LockedDoorToArena:
      lock CourageKey

    node MoldormArena:
      enemy Moldorm1
      enemy Moldorm2

    node StairsAfterMoldorm:
      door ToB1Arena

    Entrance <-> BehindBombableWall: (item Bombs | item Bombchus)

    Entrance -> LockedDoorToArena
    MoldormArena -> LockedDoorToArena: (defeated Moldorm1 & defeated Moldorm2)
    LockedDoorToArena -> Entrance: open CourageKey
    LockedDoorToArena -> MoldormArena: open CourageKey

    MoldormArena -> StairsAfterMoldorm: (defeated Moldorm1 & defeated Moldorm2)
    StairsAfterMoldorm -> MoldormArena: setting MirroredArenaDoors


    node PolsVoiceStairs:
      door ToB1PolsVoice

    node PolsVoiceArea:
      enemy PolsVoice
      flag PolsDoorOpen
      flag KnowCourageTempleOrder

    PolsVoiceStairs <-> PolsVoiceArea: (state BlueSwitch & lose RedSwitch)
    # We can't go through if the current state is red, so crossing that point loses the possibility of the switch being red, but still needs the blue state to pass (in case it was already lost before)

    node PolsBombableWall:
      chest PolsLeft
      chest PolsRight
      flag InvisibleMazePath

    PolsVoiceArea <-> PolsBombableWall: (item Bombs | item Bombchus)

    node PolsVoiceKill:
      chest PolsKey

    node LockedDoorToF2:
      lock CourageKey

    node AfterLeftLockedDoor:
      door ToF2ElectricMaze

    PolsVoiceArea -> Entrance
    Entrance -> PolsVoiceArea: flag PolsDoorOpen
    PolsVoiceArea <-> PolsVoiceKill: defeated PolsVoice

    PolsVoiceArea -> LockedDoorToF2
    AfterLeftLockedDoor -> LockedDoorToF2
    LockedDoorToArena -> PolsVoiceArea: open CourageKey
    LockedDoorToArena -> AfterLeftLockedDoor: open CourageKey

    node AfterSquareCrystal:
      door ToB1InvisibleMaze

    node PolsSquarePedestal:
      gain SquarePedestalPolsVoice
    # Placed the Crystal on the pedestal

    PolsVoiceArea <-> PolsSquarePedestal: (state SquareCrystal & lose SquareCrystal)
    # Can't put the crystal on the pedestal if we don't have it, and after placing it, we don't have it anymore
    PolsVoiceArea <-> AfterSquareCrystal: state SquarePedestalPolsVoice
    # The crystal has to be placed on that pedestal to go through


    node LeftmostStairsF1:
      door ToB1Leftmost

    node BottomLeft

    node EyeTargetDoor:
      flag BottomLeftDoor

    LeftmostStairsF1 <-> BottomLeft: (state RedSwitch & lose BlueSwitch)

    BottomLeft <-> EyeTargetDoor: item Bow
    BottomLeft <-> PolsVoiceArea: flag BottomLeftDoor

    node StairsToF2Octorok:
      door ToF2Octorok

    node LeftSquarePedestal:
      gain SquarePedestalBottomLeft

    BottomLeft <-> LeftSquarePedestal: (state SquarePedestal & lose SquarePedestal)
    BottomLeft <-> StairsToF2Octorok: state SquarePedestalBottomLeft


    node BottomRightArea:
      door ToF2SpikeTraps
      door ToB1Torches

    BottomRightArea -> Entrance


    node TopRight:
      door ToB1AfterTorches
      enemy PolsVoiceTopRight

    node TopRightPolsVoiceDefeat:
      chest PolsKeyTopRight

    node TopRightLockedDoor:
      lock CourageKey

    node StairsToF2EmptyRoom:
      door ToF2EmptyRoom

    TopRight <-> TopRightPolsVoiceDefeat: defeated PolsVoiceTopRight

    TopRight -> TopRightLockedDoor
    StairsToF2EmptyRoom -> TopRightLockedDoor
    TopRightLockedDoor -> TopRight: open CourageKey
    TopRightLockedDoor -> StairsToF2EmptyRoom: open CourageKey


  room B1:
    node MiddleMovingPlatform:
      door ToF1Arena
      door ToF1PolsVoice

    node ColorSwitchBottom: # Leaving this point, we can be at any of the two state
      gain BlueSwitch
      gain RedSwitch

    MiddleMovingPlatform <-> ColorSwitchBottom: (item Bow | item Boomerang | item Hammer)

    node InvisibleMaze:
      door ToF1InvisibleMaze

    node AfterInvisibleMaze:
      chest BowChest
      chest IslandChest

    node LeftmostStairsB1:
      door ToF1Leftmost

    node ColorSwitchLeft:
      gain BlueSwitch
      gain RedSwitch

    LeftmostStairsB1 <-> ColorSwitchLeft: item Bow
    MiddleMovingPlatform -> AfterInvisibleMaze: (item GrapplingHook & flag InvisibleMazePath)
    InvisibleMaze <-> AfterInvisibleMaze: flag InvisibleMazePath
    AfterInvisibleMaze -> LeftmostStairsB1: item Bow


    node Torches:
      door ToF1Torches

    node AfterTorches

    Torches -> AfterTorches: item Boomerang

    node Target:
      chest EyeTarget

    node ColorSwitchRight:
      gain BlueSwitch
      gain RedSwitch

    AfterTorches <-> Target: (item Bow & item Boomerang)
    AfterTorches <-> ColorSwitchRight: (item Bow & item Boomerang)

    node StairsToF1TopRight:
      door ToF1TopRight

    AfterTorches <-> StairsToF1TopRight: (state BlueSwitch & lose RedSwitch)


  room F2:
    node ElectricMaze:
      door ToF1ElectricMaze

    node AfterElectricMaze:
      chest AfterUDRL

    ElectricMaze <-> AfterElectricMaze: (flag KnowCourageTempleOrder | setting NoPuzzleSolution)

    node Octoroks:
      door ToF1Octorok
      enemy Octorok1
      enemy Octorok2
      enemy Octorok3
      enemy Octorok4
      enemy Octorok5
      enemy Octorok6
      enemy Octorok7

    node ChestAfterEyes:
      chest TwoEyes

    node StairsAfterSpikeTraps:
      chest SpikeTraps
      door ToF1SpikeTraps

    Octoroks -> ChestAfterEyes: (defeated Octorok1 & defeated Octorok2 & defeated Octorok3 & defeated Octorok4 & defeated Octorok5 & defeated Octorok6 & defeated Octorok7 & item Bow)
    Octoroks -> StairsAfterSpikeTraps: (defeated Octorok1 & defeated Octorok2 & defeated Octorok3 & defeated Octorok4 & defeated Octorok5 & defeated Octorok6 & defeated Octorok7 & (item Bombs | item Bombchus))


    node EmptyRoom:
      door ToF1EmptyRoom

    node BossKeyChest:
      chest BossKey

    node StairsToF3:
      door ToF3

    EmptyRoom <-> BossKeyChest: (item Bow & (flag KnowCourageTempleOrder | setting NoPuzzleSolution))
    EmptyRoom <-> StairsToF3: (item CourageBossKey & (flag KnowCourageTempleOrder | setting NoPuzzleSolution))


  room F3:
    node Main:
      door ToF2
      entrance BlueWarp
      door ToBoss

    node ChestBehindWall:
      chest Hidden

    Main <-> ChestBehindWall: (item Bombs | item Bombchus)
